USE [CapInvRepository]
GO
/****** Object:  StoredProcedure [dbo].[USP_CAPFCST_AD_CONSUMPTION]    Script Date: 01-08-2017 10:28:37 ******/
IF ( OBJECT_ID('dbo.USP_CAPFCST_AD_CONSUMPTION', 'P') IS NOT NULL ) 
   DROP PROCEDURE dbo.USP_CAPFCST_AD_CONSUMPTION
GO


CREATE PROCEDURE [dbo].[USP_CAPFCST_AD_CONSUMPTION]
/****************************************************************************************************
* COPYRIGHT REVENUE ANALYTICS 2017
* ALL RIGHTS RESERVED
*
* CREATED BY: DATA ENGINEERING
* FILENAME:   USP_CAPFCST_AD_CONSUMPTION.SQL
* DATE:       04/07/2017
*
* APPLICATION:  CREATES ANALYSIS DATA SET FOR CONSUMPTION DATA
*               
* PARAMETERS:   @V_PARENT_PROCESS_ID == PARENT OR CALLING PROCESS ID
*				@V_ERROR_CODE		== RETURNED ERROR CODE
*  				
* ASSUMPTIONS: BOOKING_FINAL, ROUTELINKS AND SCHEDULE TABLES UPDATED
*
* INPUT TABLE(S):	BOOKING_FINAL
*					ROUTELINKS
*					SCHEDULE
* OUTPUT TABLE(S):	CAPFCST_AD_CONSUMPTION
*					CAPFCST_CONSUMPTION_NOREEF
*
* PARAMETERS:   @V_RUNDATE = DATE OF THE RUN
*				@V_PARENT_PROCESS_ID == PARENT OR CALLING PROCESS ID
*				@V_ERROR_CODE		== RETURNED ERROR CODE
*
* Example call: DECLARE	@return_value int,@V_ERROR_CODE int
*
*				EXEC	@return_value = [dbo].[USP_CAPFCST_AD_CONSUMPTION]
*						@V_RUNDATE = '2017-04-01',
						@V_SERVICE = '84K',
						@V_PARENT_PROCESS_ID = 0,
*						@V_ERROR_CODE = @V_ERROR_CODE OUTPUT
*
*****************************************************************************************************
* NOTES:
*  NAME					CREATED        LAST MOD			COMMENTS
*  DE\ELM               4/17/2017						PROCEDURE CREATED
*  EM									6/26/2017       Added capability of running 13 weeks data 
														(Rundate)          
****************************************************************************************************/
	 @V_RUNDATE DATE
	 ,@V_SERVICE VARCHAR(10)
	,@V_PARENT_PROCESS_ID INT
	,@V_ERROR_CODE INT OUTPUT
AS
BEGIN
	SET NOCOUNT ON 
	--DECLARE VARIABLES
	DECLARE @V_ERRORMESSAGE NVARCHAR(4000)  
	DECLARE @V_MSG  NVARCHAR(4000)
	DECLARE @V_NOTE  NVARCHAR(4000)
	DECLARE @V_ERRORSEVERITY INT  

	DECLARE @V_PROCESS_ID INT = NEXT VALUE FOR DBO.SEQ_LOG_PROCESS_ID
	DECLARE @V_PROCESS_NAME VARCHAR(255) = OBJECT_SCHEMA_NAME(@@PROCID) + '.' + OBJECT_NAME(@@PROCID)
	DECLARE @V_FIANLERROR INT
	DECLARE @V_SYSTEM_USER NVARCHAR(255) = SYSTEM_USER
	DECLARE @V_SYSTEMTIME DATETIME = GETUTCDATE()
	DECLARE @V_RECORD_CNT INT

	DECLARE @V_STEP_ID TINYINT
	DECLARE @V_STEP_NAME VARCHAR(255)
	DECLARE @V_AUDITVERSIONID INT

	DECLARE @V_IS_VSA_F_FLAG VARCHAR(2) = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'IS_VSA_F_FLAG')
	DECLARE @V_SHIPMENT_STATUS_FLAG VARCHAR(25) = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'SHIPMENT_STATUS_FLAG')
	
	DECLARE @V_LOOKBACK_DAYS INT = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'LOOKBACK_DAYS')
	DECLARE @V_LOOKFORWARD_DAYS INT = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'LOOKFORWARD_DAYS')

	DECLARE @V_FFE_TO_TEU_MULTIPLIER INT = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'FFE_TO_TEU_MULTIPLIER')
	DECLARE @V_PLUGS_FOR_20 INT = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'PLUGS_FOR_20')
	DECLARE @V_PLUGS_FOR_40 INT = (SELECT CAST(PARAMVALUE AS VARCHAR) FROM ANALYTICSDATAMART.[DBO].[CAPINVUDF_APP_PARAMETER] WHERE UPPER(PARAMNAME) = 'PLUGS_FOR_40')

	--ASSIGN TODAYS DATE IF RUNDATE IS NULL
	SET @V_RUNDATE = CAST(COALESCE(@V_RUNDATE, GETUTCDATE()) AS DATE)
	SET @V_PARENT_PROCESS_ID = COALESCE(@V_PARENT_PROCESS_ID,0)



	--GET AUDIT VERSION ID
	EXEC @V_AUDITVERSIONID = ANALYTICSDATAMART.DBO.USP_GETAUDITVERSIONID @V_RUNDATE, @V_AUDITVERSIONID OUTPUT

	BEGIN TRY
		--LOG PROCESS		
		EXEC DBO.USP_LOG_PROCESS @V_PROCESS_ID = @V_PROCESS_ID
							, @V_PROCESS_NAME = @V_PROCESS_NAME
							, @V_USER_NAME = @V_SYSTEM_USER
							, @V_START_DT = @V_SYSTEMTIME
							, @V_END_DT = NULL
							, @V_STATUS = 'RUNNING'
							, @V_ACTION = 1
							, @V_PARENT_PROCESS_ID = @V_PARENT_PROCESS_ID
							, @V_NOTE = 'PROCESS CONSUMPTION DATA'
							, @V_START_STEP = 1;

		SET @V_STEP_ID = 10
		SET @V_STEP_NAME = 'COMBINE BOOKINGS AND ROUTE LINKS'
		SET @V_NOTE = 'COMBINE THE BOOKINGS AND THE SEGMENTS FOR THOSE BOOKINGS FROM THE ROUTELINKS TABLE'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = @V_SYSTEMTIME
  					, @V_END_DT = NULL
  					, @V_STATUS = 'RUNNING'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 1
  					, @V_NOTE = @V_NOTE;

		IF OBJECT_ID('DBO.TMPCONSUMPTIONSTEP1', 'U') IS NOT NULL
		  DROP TABLE DBO.TMPCONSUMPTIONSTEP1;

		SELECT L.[SHIPMENT_NO_X]
			  ,L.[SHIPMENT_VRSN_ID_X]
			  ,L.[BRAND]
			  ,L.[CONTAINER_SIZE_X]
			  --,L.[CONTAINER_TYPE_X]
			  ,L.[LOPFI]
			  ,L.[DIPLA]
			  ,L.[POD]
			  ,L.[POR]
			  ,L.[PRICE_CALC_BASE_LCL_D] AS PCD_DATE
			  ,L.[BOOKING_DATE]
			  ,L.[STRING_ID_X]
			  ,L.[ROUTE_CD]
			  ,L.[SHIPMENT_STATUS]
			  --,L.[IS_LIVE_REEFER]
			  --,L.[NAC_CUSTOMER]
			  ,R.VESSELCODE
			  ,R.SERVICECODE
			  ,R.FROMSITECODE
			  ,R.TOSITECODE
			  ,R.LOCSEQX
			  ,R.DEPVOYAGEX
			  ,R.ARRVOYAGEX
			  ,R.DEPLOCALEXPECTEDDT AS ETD
			  , CARGO_TYPE
			  ,SUM(L.[FFE]) AS FFE
			  ,AVG([GROSSWEIGHT]) AS [GROSSWEIGHTKG]
		INTO 
			DBO.TMPCONSUMPTIONSTEP1
		FROM 	
			ANALYTICSDATAMART.[DBO].[BOOKING_FINAL] L 
				INNER JOIN ANALYTICSDATAMART.DBO.ROUTELINKS R 
		ON 
			L.SHIPMENT_NO_X = R.SHIPMENT_NO_X
			AND L.SHIPMENT_VRSN_ID_X = R.SHIPMENT_VRSN_ID_X
		WHERE 
			UPPER(L.IS_VSA_F) IN (@V_IS_VSA_F_FLAG)
			AND UPPER(L.SHIPMENT_STATUS) IN (@V_SHIPMENT_STATUS_FLAG)
			AND R.DEPLOCALEXPECTEDDT BETWEEN DATEADD(DAY, -@V_LOOKBACK_DAYS, @V_RUNDATE) AND DATEADD(DAY, @V_LOOKFORWARD_DAYS,@V_RUNDATE)
			--AND R.SERVICECODE = '84K'
			AND (case when @V_SERVICE is null or upper(@V_SERVICE) = 'ALL' then '1' else R.SERVICECODE end
						= case when @V_SERVICE is null or upper(@V_SERVICE) = 'ALL' then '1' else @V_SERVICE  end)
		GROUP BY 	  L.[SHIPMENT_NO_X]
			  ,L.[SHIPMENT_VRSN_ID_X]
			  ,L.[BRAND]
			  ,L.[CONTAINER_SIZE_X]
			  --,L.[CONTAINER_TYPE_X]

			  ,L.[LOPFI]
			  ,L.[DIPLA]
			  ,L.[POD]
			  ,L.[POR]
			  ,L.[PRICE_CALC_BASE_LCL_D] 
			  ,L.[BOOKING_DATE]
			  ,L.[STRING_ID_X]
			  ,L.[ROUTE_CD]
			  ,L.[SHIPMENT_STATUS]
			  --,L.[IS_LIVE_REEFER]
			  --,L.[NAC_CUSTOMER]
			  ,R.VESSELCODE
			  ,R.SERVICECODE
			  ,R.FROMSITECODE
			  ,R.TOSITECODE
			  ,R.LOCSEQX
			  ,R.DEPVOYAGEX
			  ,R.ARRVOYAGEX
			  , DEPLOCALEXPECTEDDT
			  , CARGO_TYPE

		SET @V_RECORD_CNT = @@ROWCOUNT
		SET @V_SYSTEMTIME = GETUTCDATE()
			
		--UPDATE DETAIL 
		SET @V_NOTE = @V_NOTE +' COMPLETELY SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'COMPLETED'
  					, @V_ROWS_PROCESSED = @V_RECORD_CNT
  					, @V_ACTION = 2
  					, @V_NOTE = @V_NOTE;
		---------------------------------------------------------
		--GET FIRST AND LAST LEG SEQUENCE ID FOR EACH SEGMENT
		---------------------------------------------------------
		SET @V_STEP_ID = 20
		SET @V_STEP_NAME = 'GET THE LEGS INFORMATION FOR THE ROUTE'
		SET @V_NOTE = 'USING THE SCHEDULE GET THE FIRST AND LAST LEG SEQUENCE'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = @V_SYSTEMTIME
  					, @V_END_DT = NULL
  					, @V_STATUS = 'RUNNING'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 1
  					, @V_NOTE = 'USING THE SCHEDULE GET THE FIRST AND LAST LEG SEQUENCE';



		SELECT A.VESSELCODE,A.DEPVOYAGEX AS DEPVOYAGE, A.SERVICECODE, A.FROMSITECODE,MIN(B.SCHEDULEID)  AS FIRSTSCHEDULEID
		INTO #TMP_MIN_SEQ
		FROM DBO.TMPCONSUMPTIONSTEP1 A
		INNER JOIN ANALYTICSDATAMART.DBO.ROUTELEGS_SCHEDULE B
		ON A.VESSELCODE = B.VESSELCODE
		--AND A.DEPVOYAGEX = B.VOYAGE --Removed becasue you want to we want leg across voyages
		AND A.SERVICECODE = B.SERVICECODE
		AND A.FROMSITECODE = B.DEPARTUREPORT
		GROUP BY A.VESSELCODE,A.DEPVOYAGEX, A.SERVICECODE, A.FROMSITECODE

		SELECT	gg.VESSELCODE
				,gg.DEPVOYAGE
				,gg.SERVICECODE
				,gg.FROMSITECODE
				,A.TOSITECODE
				,gg.FIRSTSCHEDULEID
				,MIN(B.SCHEDULEID)  AS LASTSCHEDULEID
		INTO #TMP_ROUTE_SEQ
		FROM DBO.TMPCONSUMPTIONSTEP1 A
		INNER JOIN #TMP_MIN_SEQ GG
		ON A.VESSELCODE = GG.VESSELCODE
		AND A.SERVICECODE = GG.SERVICECODE
		--AND A.DEPVOYAGEX = GG.DEPVOYAGE--Removed becasue you want to we want leg across voyages
		AND A.FROMSITECODE = GG.FROMSITECODE
		INNER JOIN ANALYTICSDATAMART.DBO.ROUTELEGS_SCHEDULE B
		ON A.VESSELCODE = B.VESSELCODE
		AND A.SERVICECODE = B.SERVICECODE
		AND A.TOSITECODE = B.ARRIVALPORT
		AND B.SCHEDULEID >= GG.FIRSTSCHEDULEID
		GROUP BY gg.VESSELCODE
				,gg.DEPVOYAGE
				,gg.SERVICECODE
				,gg.FROMSITECODE
				,A.TOSITECODE
				,gg.FIRSTSCHEDULEID

		IF OBJECT_ID('DBO.TMPCONSUMPTION_LEGS', 'U') IS NOT NULL
			DROP TABLE DBO.TMPCONSUMPTION_LEGS;

		SELECT 	  L.[SHIPMENT_NO_X]
				  ,L.[SHIPMENT_VRSN_ID_X]
				  ,L.[BRAND]
				  ,L.[CONTAINER_SIZE_X]
				  ,L.[FFE]
				  --,L.[CONTAINER_TYPE_X]
				  ,L.CARGO_TYPE
				  ,L.[GROSSWEIGHTKG]
				  ,L.[LOPFI]
				  ,L.[DIPLA]
				  ,L.[POD]
				  ,L.[POR]
				  ,L.PCD_DATE
				  ,L.[BOOKING_DATE]
				  ,L.[STRING_ID_X]
				  ,L.[ROUTE_CD]
				  ,L.[SHIPMENT_STATUS]
				  --,L.[IS_LIVE_REEFER]
				  --,L.[NAC_CUSTOMER]
				  ,L.VESSELCODE
				  ,L.SERVICECODE
				  ,L.FROMSITECODE
				  ,L.TOSITECODE
				  ,L.LOCSEQX
				  ,L.DEPVOYAGEX
				  ,L.ETD
				  ,LL.FIRSTSCHEDULEID
				  ,LL.LASTSCHEDULEID
		INTO DBO.TMPCONSUMPTION_LEGS
		FROM DBO.TMPCONSUMPTIONSTEP1 L
		INNER JOIN #TMP_ROUTE_SEQ LL
		ON L.VESSELCODE = LL.VESSELCODE
		AND L.DEPVOYAGEX = LL.DEPVOYAGE
		AND L.SERVICECODE = LL.SERVICECODE
		AND L.FROMSITECODE = LL.FROMSITECODE
		AND L.TOSITECODE = LL.TOSITECODE;

		SET @V_RECORD_CNT = @@ROWCOUNT
		SET @V_SYSTEMTIME = GETUTCDATE()
			
		--UPDATE DETAIL 
		SET @V_NOTE = @V_STEP_NAME +' COMPLETELY SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'COMPLETED'
  					, @V_ROWS_PROCESSED = @V_RECORD_CNT
  					, @V_ACTION = 2
  					, @V_NOTE = @V_NOTE;

		---------------------------------------------------------
		--ASSIGN LEGS TO EACH SEGMENT
		---------------------------------------------------------
		SET @V_STEP_ID = 25
		SET @V_STEP_NAME = 'ASSIGN LEGS TO EACH SEGMENT'
		SET @V_NOTE = 'USING THE SCHEDULE ASSIGN LEGS TO EACH ROUTE SEGMENTS'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = @V_SYSTEMTIME
  					, @V_END_DT = NULL
  					, @V_STATUS = 'RUNNING'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 1
  					, @V_NOTE = @V_NOTE;

		IF OBJECT_ID('DBO.TMPCONSUMPTIONSTEP2', 'U') IS NOT NULL
			DROP TABLE DBO.TMPCONSUMPTIONSTEP2;

		SELECT  L.[SHIPMENT_NO_X]
				,L.[SHIPMENT_VRSN_ID_X]
				,L.[BRAND]
				,L.[CONTAINER_SIZE_X]
				,L.[FFE]
				--,L.[CONTAINER_TYPE_X]
				,L.CARGO_TYPE
				,L.[GROSSWEIGHTKG]
				,L.[LOPFI]
				,L.[DIPLA]
				,L.[POD]
				,L.[POR]
				,L.PCD_DATE
				,L.[BOOKING_DATE]
				,L.[STRING_ID_X]
				,L.[ROUTE_CD]
				,L.[SHIPMENT_STATUS]
				--,L.[IS_LIVE_REEFER]
				--,L.[NAC_CUSTOMER]
				,L.VESSELCODE
				,L.SERVICECODE
				,L.FROMSITECODE
				,L.TOSITECODE
				,L.LOCSEQX
				,R.VOYAGE AS VOYAGE
				,L.ETD
				,R.SCHEDULEID
				,R.DEPARTUREPORT
				,R.ARRIVALPORT
				,R.ARRIVALDATE
				,R.LEGSEQID
				,R.[ORIGINAL_ETD]
				,R.[ACTUAL_ETD]
				,R.serviceCodeDirection
		INTO DBO.TMPCONSUMPTIONSTEP2
		FROM DBO.TMPCONSUMPTION_LEGS L 
		INNER JOIN ANALYTICSDATAMART.DBO.ROUTELEGS_SCHEDULE R 
		ON ISNULL(LTRIM(RTRIM(L.SERVICECODE)),0) = ISNULL(LTRIM(RTRIM(R.SERVICECODE)),0)
			AND ISNULL(LTRIM(RTRIM(L.VESSELCODE)),0) = ISNULL(LTRIM(RTRIM(R.VESSELCODE)),0)
					AND R.SCHEDULEID BETWEEN L.FIRSTSCHEDULEID AND L.LASTSCHEDULEID;

		SET @V_RECORD_CNT = @@ROWCOUNT
		SET @V_SYSTEMTIME = GETUTCDATE()
			
		--UPDATE DETAIL 
		SET @V_NOTE = @V_STEP_NAME + ' CCOMPLETED SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'COMPLETED'
  					, @V_ROWS_PROCESSED = @V_RECORD_CNT
  					, @V_ACTION = 2
  					, @V_NOTE = @V_NOTE;

		---------------------------------------------------------
		-- IN THIS STEP WE TAKE THE SITECODES BROUGHT IN FROM SCHEDULES DATA 
		-- TO CREATE THE FROM AND TO SITECODE 
		--ADDITIONALLY WE POPULATE THE ARRIVAL AND DEP DATES AND ADD CALCULATED FIELDS
		---------------------------------------------------------
		SET @V_STEP_ID = 30
		SET @V_STEP_NAME = 'CALCULTE NUMBER OF CONTAINERS, FFE AND PLUGS'
		SET @V_NOTE = 'AT THE LEG LEVEL CALCULATE THE TEU, FFE, PLUGS AND NUMBER OF CONTAINER'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = @V_SYSTEMTIME
  					, @V_END_DT = NULL
  					, @V_STATUS = 'RUNNING'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 1
  					, @V_NOTE = @V_NOTE;

		IF OBJECT_ID('DBO.TMPCONSUMPTIONSTEP3', 'U') IS NOT NULL
		  DROP TABLE DBO.TMPCONSUMPTIONSTEP3;

		SELECT   L.SHIPMENT_NO_X
			,L.SHIPMENT_VRSN_ID_X
			,L.BOOKING_DATE
			,L.ROUTE_CD
			,L.SERVICECODE
			,L.STRING_ID_X
			,L.VESSELCODE AS VESSEL
			,L.VOYAGE
			,L.LEGSEQID
			,L.serviceCodeDirection
			,L.CONTAINER_SIZE_X
			--,L.CONTAINER_TYPE_X
			,L.CARGO_TYPE
			,L.LOPFI
			,L.DIPLA
			,L.FROMSITECODE
			,L.TOSITECODE
			,L.SCHEDULEID
			,LTRIM(RTRIM(L.DEPARTUREPORT)) AS FROMLEGSITECODE
			,L.ARRIVALPORT AS TOLEGSITECODE
			,COALESCE(L.ORIGINAL_ETD,L.ETD) AS DEPARTUREDATE
			,L.ETD
			,L.ARRIVALDATE
			,L.FFE * 2 AS TEU
			,L.GROSSWEIGHTKG
			,CASE WHEN UPPER(CARGO_TYPE) = 'REEF' AND L.CONTAINER_SIZE_X = 20 THEN @V_PLUGS_FOR_20
				 WHEN UPPER(CARGO_TYPE) = 'REEF' AND L.CONTAINER_SIZE_X = 40 THEN @V_PLUGS_FOR_40
				 ELSE 0
			END AS PLUGS
			,CASE WHEN L.CONTAINER_SIZE_X = 20 THEN FFE * @V_FFE_TO_TEU_MULTIPLIER
				 WHEN L.CONTAINER_SIZE_X = 40 THEN FFE
				 ELSE FFE
			END AS CONTAINERCOUNT
		INTO 
			DBO.TMPCONSUMPTIONSTEP3 
		FROM DBO.TMPCONSUMPTIONSTEP2 L;

		SET @V_RECORD_CNT = @@ROWCOUNT
		SET @V_SYSTEMTIME = GETUTCDATE()
			
		--UPDATE DETAIL 
		SET @V_NOTE = @V_STEP_NAME + ' COMPLETED SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'COMPLETED'
  					, @V_ROWS_PROCESSED = @V_RECORD_CNT
  					, @V_ACTION = 2
  					, @V_NOTE = @V_NOTE;
		---------------------------------------------------------
		--THIS FINAL STEP INCORPORATES THE FREESALE FLAG BASED ON COMMITMENT DATA
		---------------------------------------------------------
		SET @V_STEP_ID = 40
		SET @V_STEP_NAME = 'BUILD CONSUMPTION ANALYSIS DATA SET'
		SET @V_NOTE = 'ADD THE FREESALE FLAG AND CREATE THE ANALYSIS DATASET'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = @V_SYSTEMTIME
  					, @V_END_DT = NULL
  					, @V_STATUS = 'RUNNING'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 1
  					, @V_NOTE = @V_NOTE;

		IF OBJECT_ID('DBO.CAPFCST_AD_CONSUMPTION', 'U') IS NOT NULL
		  DROP TABLE DBO.CAPFCST_AD_CONSUMPTION;

		SELECT DISTINCT @V_RUNDATE as RUNDATE
			,@V_AUDITVERSIONID as RUNID
			,L.SHIPMENT_NO_X
			,L.SHIPMENT_VRSN_ID_X
			,L.BOOKING_DATE
			,L.ROUTE_CD
			,L.SERVICECODE
			,L.STRING_ID_X
			,L.VESSEL
			,L.VOYAGE
			,L.LEGSEQID
			,L.CONTAINER_SIZE_X
			--,L.CONTAINER_TYPE_X
			,L.CARGO_TYPE
			,L.LOPFI
			,L.DIPLA
			,L.FROMSITECODE
			,L.TOSITECODE
			,L.SCHEDULEID
			,L.FROMLEGSITECODE
			,L.TOLEGSITECODE
			,L.DEPARTUREDATE
			,L.ETD
			,L.ARRIVALDATE
			,L.TEU
			,L.GROSSWEIGHTKG
			,L.PLUGS
			,L.CONTAINERCOUNT			
			,L.serviceCodeDirection
			, CAST(CASE WHEN R.Cargo_Type IS NOT NULL THEN 0 ELSE 1 END AS TINYINT) AS ISFREESALE
			, FFEConsumed
			, FFENotConsumed
		INTO 
			DBO.CAPFCST_AD_CONSUMPTION 
		FROM 
			DBO.TMPCONSUMPTIONSTEP3 L 
			LEFT OUTER JOIN 
			(
				select SHIPMENT_NO_X,CONTAINER_SIZE_X,Cargo_Type,SUM(FFEConsumed) as FFEConsumed,SUM(FFENotConsumed) as FFENotConsumed
				from ANALYTICSDATAMART.DBO.BOOKINGCOMMITMENT (NOLOCK)
				group by SHIPMENT_NO_X,CONTAINER_SIZE_X,Cargo_Type
			) R 
		ON 
			L.SHIPMENT_NO_X = R.SHIPMENT_NO_X
			AND L.CONTAINER_SIZE_X = R.CONTAINER_SIZE_X
			AND L.CARGO_TYPE = R.CARGO_TYPE

		SET @V_RECORD_CNT = @@ROWCOUNT
		SET @V_SYSTEMTIME = GETUTCDATE()
			
		--UPDATE DETAIL 
		SET @V_NOTE = @V_STEP_NAME + ' COMPLETED SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'COMPLETED'
  					, @V_ROWS_PROCESSED = @V_RECORD_CNT
  					, @V_ACTION = 2
  					, @V_NOTE = @V_NOTE;

		CREATE CLUSTERED INDEX CDX_CAPFCST_AD_CONSUMPTION ON DBO.CAPFCST_AD_CONSUMPTION(BOOKING_DATE,VESSEL,VOYAGE,SHIPMENT_NO_X,SHIPMENT_VRSN_ID_X, SERVICECODE,LEGSEQID,FROMLEGSITECODE,TOLEGSITECODE,DEPARTUREDATE,ARRIVALDATE)

		--UPDATE PROCESS --COMPLETE PROCESS
		SET @V_NOTE = @V_PROCESS_NAME + ' COMPLETED SUCCESSFULLY'
		EXEC DBO.USP_LOG_PROCESS @V_PROCESS_ID = @V_PROCESS_ID
							, @V_PROCESS_NAME = @V_PROCESS_NAME
							, @V_USER_NAME = @V_SYSTEM_USER
							, @V_START_DT = NULL
							, @V_END_DT = @V_SYSTEMTIME
							, @V_STATUS = 'COMPLETED'
							, @V_ACTION = 2
							, @V_PARENT_PROCESS_ID = NULL
							, @V_NOTE = @V_NOTE
							, @V_START_STEP = 1;

			SET @V_ERROR_CODE = 0
			RETURN @V_ERROR_CODE
	END TRY
	BEGIN CATCH
		SET @V_MSG = COALESCE(ERROR_PROCEDURE() +' LINE:'+CAST(ERROR_LINE() AS VARCHAR(10))+' MESSAGE:' +ERROR_MESSAGE(),'')
		SET @V_ERRORMESSAGE = 'PROCESS TO REFRESH BOOKING FINAL FAILED: ERR:' + @V_MSG
		SET @V_ERRORSEVERITY = ERROR_SEVERITY()  
		SET @V_SYSTEMTIME = GETUTCDATE()
		
		--UPDATE DETAIL 
		EXEC DBO.USP_LOG_PROCESS_DETAIL @V_PROCESS_ID = @V_PROCESS_ID
  					, @V_STEP_ID = @V_STEP_ID
  					, @V_STEP_NAME = @V_STEP_NAME
  					, @V_START_DT = NULL
  					, @V_END_DT = @V_SYSTEMTIME
  					, @V_STATUS = 'FAILED'
  					, @V_ROWS_PROCESSED = NULL
  					, @V_ACTION = 2
  					, @V_NOTE = @V_ERRORMESSAGE
  					;

		--UPDATE PROCESS --COMPLETE PROCESS
		EXEC DBO.USP_LOG_PROCESS @V_PROCESS_ID = @V_PROCESS_ID
							, @V_PROCESS_NAME = @V_PROCESS_NAME
							, @V_USER_NAME = @V_SYSTEM_USER
							, @V_START_DT = NULL
							, @V_END_DT = @V_SYSTEMTIME
							, @V_STATUS = 'FAILED'
							, @V_ACTION = 2
							, @V_PARENT_PROCESS_ID = NULL
							, @V_NOTE =  @V_ERRORMESSAGE
							, @V_START_STEP = 1
							;  

		SET @V_ERROR_CODE = -1
		RETURN @V_ERROR_CODE
	END CATCH
END
